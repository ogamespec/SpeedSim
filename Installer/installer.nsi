; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "SpeedSim"
!define PRODUCT_VERSION "0.9.8.1b"
!define PRODUCT_WEB_SITE "http://www.speedsim.net/"
!define PRODUCT_FORUM "http://forum.speedsim.net"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\SpeedSim.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
;!define UNICODE
!ifdef UNICODE
  !define SUB_FOLDER "unicode"
!else
  !define SUB_FOLDER "ansi"
!endif

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "installer.ico"
!define MUI_UNICON "deinstaller.ico"

!define MUI_WELCOMEFINISHPAGE_BITMAP "bg.bmp"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!define MUI_LICENSEPAGE_TEXT_BOTTOM "This is the license for the case you want to modify/distribute the program. In this case you have to accept it."
!define MUI_LICENSEPAGE_BUTTON "Next >"
!insertmacro MUI_PAGE_LICENSE "gpl.txt"
; Components
!insertmacro MUI_PAGE_COMPONENTS
; icons page
Page custom IconsPage
; start menu
var ICONS_GROUP
;!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "SpeedSim"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!define MUI_STARTMENUPAGE_TEXT_CHECKBOX "Do not create start menu folder"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN_NOTCHECKED
!define MUI_FINISHPAGE_RUN "$INSTDIR\SpeedSim.exe"
!define MUI_FINISHPAGE_TEXT $(FinishText)
!define MUI_FINISHPAGE_LINK "Visit the SpeedSim Forum"
!define MUI_FINISHPAGE_LINK_LOCATION ${PRODUCT_FORUM}
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "SpeedSim_${PRODUCT_VERSION}_${SUB_FOLDER}.exe"
InstallDir "$PROGRAMFILES\SpeedSim"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

ReserveFile "icons.ini"
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS
Var CHECK_DESKOP
Var CHECK_QUICKLAUNCH

Function .onInit
;  !insertmacro MUI_LANGDLL_DISPLAY
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "icons.ini"
FunctionEnd

Function IconsPage
  !insertmacro MUI_HEADER_TEXT "Choose Icons" "Choose now what icons you would like to show"
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "icons.ini"
FunctionEnd

Section "SpeedSim Core files" SecCore
  SectionIn RO
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "${SUB_FOLDER}\SpeedSim.exe"
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\SpeedSim.lnk" "$INSTDIR\SpeedSim.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
  File "${SUB_FOLDER}\SpeedKernel.dll"
  File "Changelog_eng.txt"
  File "Changelog.txt"
  File "gpl.txt"
  File "bwc.css"
  File "cr.css"
  File "hotkeys.txt"
  
  !insertmacro MUI_INSTALLOPTIONS_READ $CHECK_DESKOP "icons.ini" "Field 1" "State"
  !insertmacro MUI_INSTALLOPTIONS_READ $CHECK_QUICKLAUNCH "icons.ini" "Field 2" "State"
  
  StrCmp $CHECK_DESKOP "1" "" +2
    CreateShortCut "$DESKTOP\SpeedSim.lnk" "$INSTDIR\SpeedSim.exe"
  StrCmp $CHECK_QUICKLAUNCH "1" "" +2
    CreateShortCut "$QUICKLAUNCH\Start SpeedSim.lnk" "$INSTDIR\SpeedSim.exe"
SectionEnd

SectionGroup "Languages" SecLangGroup
  Section "Deutsch" SecDeutsch
     SectionIn RO
  SectionEnd

/*  Section "Bosnian-Croatian-Serbian" SecBCS
     File "${SUB_FOLDER}\ba_cro_ser_lang.ini"
  SectionEnd*/

!ifdef UNICODE
  Section "Bulgarian" SecBg
    File "${SUB_FOLDER}\bg_lang.ini"
  SectionEnd
!endif
  
  Section "Castellano" SecEs
     File "Idioma_Esp.ini"
  SectionEnd

/*  Section "Chinese" SecChs
     File "${SUB_FOLDER}\CHS_lang.ini"
  SectionEnd*/
  
/*  Section "Czech" SecCz
     File "cz_lang.ini"
  SectionEnd*/
  
  Section "Dansk" SecDk
     File "da_lang.ini"
  SectionEnd

  Section "English" SecEnglish
     File "eng_lang.ini"
  SectionEnd

  Section "Français" SecFr
     File "fr_lang.ini"
  SectionEnd
  
!ifdef UNICODE
  Section "Greek" SecGr
    File "${SUB_FOLDER}\gr_lang.ini"
  SectionEnd
!endif

/*  Section "Korean" SecKor
     File "${SUB_FOLDER}\kor_lang.ini"
  SectionEnd*/

  Section "Nederlands" SecNl
     File "nl_lang.ini"
  SectionEnd
  
  Section "Polski" SecPl
     File "${SUB_FOLDER}\polish_lang.ini"
  SectionEnd

  Section "Português" SecPt
     File "Idioma_PT.ini"
  SectionEnd

  Section "Romana" SecRo
     File "ro_lang.ini"
  SectionEnd
  
  Section "Russian" SecRus
     File "${SUB_FOLDER}\rus_lang.ini"
  SectionEnd
  
  Section "Swedish" SecSwe
     File "swe_lang.ini"
  SectionEnd
  
  Section "Türkçe" SecTr
     File "tr_lang.ini"
  SectionEnd

SectionGroupEnd

SectionGroup "IdeasPlanet" SecIP
  Section /o "Standard ship data" SecSD
     File "Standard.sd"
  SectionEnd

  Section /o "Standard RF" SecRF
     File "Standard.rf"
  SectionEnd
SectionGroupEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  WriteIniStr "$INSTDIR\forums.url" "InternetShortcut" "URL" "${PRODUCT_FORUM}"
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\SpeedSim Homepage.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Support Forums.lnk" "$INSTDIR\forums.url"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\SpeedSim.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\SpeedSim.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  ; To make it possible to uninstall SpeedSim over system control
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "DisplayName" "${PRODUCT_NAME}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "UninstallString" "$INSTDIR\uninst.exe"
  
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "NoRepair" 1
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "InstallLocation" "$INSTDIR"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) has been uninstalled successfully."
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Do you really want to uninstall $(^Name)?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER Application $R0
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\forums.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\gpl.txt"
  Delete "$INSTDIR\Changelog.txt"
  Delete "$INSTDIR\Changelog_eng.txt"
  Delete "$INSTDIR\SpeedKernel.dll"
  Delete "$INSTDIR\SpeedSim.exe"
  Delete "$INSTDIR\bwc.css"
  Delete "$INSTDIR\cr.css"
  Delete "$INSTDIR\eng_lang.ini"
  Delete "$INSTDIR\rus_lang.ini"
  Delete "$INSTDIR\fr_lang.ini"
  Delete "$INSTDIR\tr_lang.ini"
  Delete "$INSTDIR\da_lang.ini"
  Delete "$INSTDIR\kor_lang.ini"
  Delete "$INSTDIR\Idioma_PT.ini"
  Delete "$INSTDIR\Idioma_Esp.ini"
  Delete "$INSTDIR\lang_polish.ini"
  Delete "$INSTDIR\ba_cro_ser_lang.ini"
  Delete "$INSTDIR\nl_lang.ini"
  Delete "$INSTDIR\swe_lang.ini"
  Delete "$INSTDIR\ro_lang.ini"
  Delete "$INSTDIR\chs_lang.ini"
  Delete "$INSTDIR\cz_lang.ini"
  Delete "$INSTDIR\bg_lang.ini"
  Delete "$INSTDIR\gr_lang.ini"
  Delete "$INSTDIR\Standard.sd"
  Delete "$INSTDIR\Standard.rf"
  Delete "$INSTDIR\BW.htm"
  Delete "$INSTDIR\kb.htm"
  Delete "$INSTDIR\hotkeys.txt"

  Delete "$SMPROGRAMS\$R0\Uninstall.lnk"
  Delete "$SMPROGRAMS\$R0\SpeedSim Homepage.lnk"
  Delete "$SMPROGRAMS\$R0\Support Forums.lnk"
  Delete "$SMPROGRAMS\$R0\SpeedSim.lnk"
  Delete "$DESKTOP\SpeedSim.lnk"
  Delete "$QUICKLAUNCH\Start SpeedSim.lnk"

  RMDir "$SMPROGRAMS\$R0"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd

LangString DESC_SecCore ${LANG_ENGLISH} "SpeedSim Core and GUI (required)"
LangString DESC_SecDeutsch ${LANG_ENGLISH} "German language (always implemented)"
LangString DESC_SecEnglish ${LANG_ENGLISH} "English language file (required for ogame.org)"
LangString DESC_SecRus ${LANG_ENGLISH} "Russian language file (required for ogame.ru)"
LangString DESC_SecFr ${LANG_ENGLISH} "French language file (required for ogame.fr)"
LangString DESC_SecTr ${LANG_ENGLISH} "Turkish language file (required for ogame.tr)"
LangString DESC_SecPt ${LANG_ENGLISH} "Portuguese language file (required for ogame.com.pt)"
LangString DESC_SecDk ${LANG_ENGLISH} "Danish language file (required for ogame.dk)"
LangString DESC_SecKor ${LANG_ENGLISH} "Korean language file (required for o-game.co.kr)"
LangString DESC_SecBCS ${LANG_ENGLISH} "Language file for Balkan OGame (ogame.com.hr)"
LangString DESC_SecNl ${LANG_ENGLISH} "Language file for Dutch OGame (ogame.nl)"
LangString DESC_SecEs ${LANG_ENGLISH} "Language file for Spanish OGame (ogame.com.es)"
LangString DESC_SecPl ${LANG_ENGLISH} "Language file for Polish OGame (ogame.pl)"
LangString DESC_SecSwe ${LANG_ENGLISH} "Language file for Swedish OGame (ogame.se)"
LangString DESC_SecRo ${LANG_ENGLISH} "Language file for Romanian OGame (ogame.ro)"
LangString DESC_SecChs ${LANG_ENGLISH} "Language file for Chinese OGame (ogame.com.cn)"
LangString DESC_SecCz ${LANG_ENGLISH} "Language file for Czech OGame (ogame.cz)"
LangString DESC_SecGr ${LANG_ENGLISH} "Language file for Greel OGame (ogame.gr)"
LangString DESC_SecBg ${LANG_ENGLISH} "Language file for Bulgarian OGame (ogame.bg)"
LangString DESC_SecLangGroup ${LANG_ENGLISH} "Language files for various OGame worlds"
LangString DESC_SecIP ${LANG_ENGLISH} "Stuff useful for ship suggestors and the IdeasPlanet of OGame"
LangString DESC_SecSD ${LANG_ENGLISH} "File with standard ship data"
LangString DESC_SecRF ${LANG_ENGLISH} "File with standard rapid fire data"
LangString FinishText ${LANG_ENGLISH} "Thank you for choosing SpeedSim.\r\n\r\nTo get other language files check out the forums on the SpeedSim homepage."

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SecCore} $(DESC_SecCore)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecDeutsch} $(DESC_SecDeutsch)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecEnglish} $(DESC_SecEnglish)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecRus} $(DESC_SecRus)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecFr} $(DESC_SecFr)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecTr} $(DESC_SecTr)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecPt} $(DESC_SecPt)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecDk} $(DESC_SecDk)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecKor} $(DESC_SecKor)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecBCS} $(DESC_SecBCS)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecNl} $(DESC_SecNl)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecEs} $(DESC_SecEs)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecPl} $(DESC_SecPl)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecSwe} $(DESC_SecSwe)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecRo} $(DESC_SecRo)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecChs} $(DESC_SecChs)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecCz} $(DESC_SecCz)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecGr} $(DESC_SecGr)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecBg} $(DESC_SecBg)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecLangGroup} $(DESC_SecLangGroup)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecIP} $(DESC_SecIP)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecSD} $(DESC_SecSD)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecRF} $(DESC_SecRF)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END
